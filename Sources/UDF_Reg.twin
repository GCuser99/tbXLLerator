'From: https://github.com/GCuser99/tbXLLerator
'Distrubuted under MIT License: https://github.com/GCuser99/tbXLLerator?tab=MIT-1-ov-file
'Copyright (c) 2026 GCUser99 (Michael Waite)

[Description("This is a helper class for registering and unregistering a UDF")]
Public Class UDF
    
    /*
    Example Usage:
    Private udfs As New Collection

    Public Function xlAutoOpen() As Long
        Dim udf As UDF
        Set udf = New UDF
        With udf
            .ProcName = "TBXLL_Multiply"
            .FuncText = "TBXLL_Multiply"
            .Category = "tB XLL UDF Add-In"
            .FuncHelp = "Basic scalar numeric binding and return"
            .Visible = True
            .Volatile = False
            .AddArgument Name:="Num1", Help:="First number"
            .AddArgument Name:="Num2", Help:="Second number"
            .Register
        End With
        udfs.Add udf ' Store in global collection for to use in AutoRemove
        xlAutoOpen = 1
    End Function
    
    Public Function xlAutoRemove() As Long
        Dim udf As UDF
        For Each udf In udfs
            udf.UnRegister
        Next udf
        xlAutoRemove = 1
    End Function
    */

    Private mProcName As String
    Private mVisible As Boolean
    Private mFuncText As String
    Private mCategory As String
    Private mShortcutText As String
    Private mHelpTopic As String
    Private mFuncHelp As String
    Private mVolatile As Boolean
    Private mThreadSafe As Boolean
    Private mMacroEquivalent As Boolean
    Private oArguments As Collection
    ' def() must persist for the lifetime of the registration (Excel retains pointers)
    Private def() As XLOPER12
    Private mRegistered As Boolean ' Private variable to track registration state to prevent double (non-fatal) unregister
    
    [Description("The symbol exported from your DLL/XLL - must match the name of the DLL procedure that Excel will call.")]
    Public Property Let ProcName(ByVal v As String)
        mProcName = v
    End Property
    Public Property Get ProcName() As String
        ProcName = mProcName
    End Property
    
    [Description("Controls whether the function is visible in Excel’s Function Wizard and autocomplete.")]
    Public Property Let Visible(ByVal v As Boolean)
        mVisible = v
    End Property
    Public Property Get Visible() As Boolean
        Visible = mVisible
    End Property
    
    [Description("Controls whether the function is marked as volatile in the worksheet.")]
    Public Property Let Volatile(ByVal v As Boolean)
        mVolatile = v
    End Property
    Public Property Get Volatile() As Boolean
        Volatile = mVolatile
    End Property
    
    [Description("Controls whether the function is registered with macro-sheet equivalent privileges, allowing it to call macro-only Excel functions such as xlSheetNm and xlfCaller. Mutually exclusive with ThreadSafe.")]
    ' Untested - searching for use-case
    Public Property Let MacroEquivalent(ByVal v As Boolean)
        mMacroEquivalent = v
    End Property
    Public Property Get MacroEquivalent() As Boolean
        MacroEquivalent = mMacroEquivalent
    End Property
    
    [Description("Controls whether the function is registered as thread-safe, allowing Excel to call it concurrently during multithreaded recalculation. Requires dynamic memory allocation pattern; incompatible with Static result variables.")]
    Public Property Let ThreadSafe(ByVal v As Boolean)
        mThreadSafe = v
    End Property
    Public Property Get ThreadSafe() As Boolean
        ThreadSafe = mThreadSafe
    End Property
    
    [Description("The name Excel exposes to the user in worksheet formulas.")]
    Public Property Let FuncText(ByVal v As String)
        mFuncText = v
    End Property
    Public Property Get FuncText() As String
        FuncText = mFuncText
    End Property
    
    [Description("Determines where the function appears in Excel’s Function Wizard.")]
    Public Property Let Category(ByVal v As String)
        mCategory = v
    End Property
    Public Property Get Category() As String
        Category = mCategory
    End Property
    
    [Description("Legacy shortcut text; ignored by modern Excel but must be supplied for compatibility.")]
    Public Property Let ShortcutText(ByVal v As String)
        mShortcutText = v
    End Property
    Public Property Get ShortcutText() As String
        ShortcutText = mShortcutText
    End Property
    
    [Description("Legacy help topic; ignored by modern Excel but must be supplied for compatibility.")]
    Public Property Let HelpTopic(ByVal v As String)
        mHelpTopic = v
    End Property
    Public Property Get HelpTopic() As String
        HelpTopic = mHelpTopic
    End Property
    
    [Description("The help string shown in Excel’s Function Wizard for the function itself.")]
    Public Property Let FuncHelp(ByVal v As String)
        mFuncHelp = v
    End Property
    Public Property Get FuncHelp() As String
        FuncHelp = mFuncHelp
    End Property
    
    [Description("Returns the collection of argument metadata for this UDF.")]
    Public Function Arguments() As Collection
        Set Arguments = oArguments
    End Function
    
    [Description("Adds a new argument definition (name + help text) to the UDF.")]
    Public Function AddArgument(Optional ByVal name As Variant, Optional ByVal help As Variant) As Argument
        Dim arg As New Argument
        arg.Name = name
        arg.Help = help
        oArguments.Add arg
        Set AddArgument = arg
    End Function
    
    [Description("Registers the UDF for use as a worksheet function.")]
    Public Sub Register()
        Dim xDll(0) As XLOPER12
        Dim dummy(0) As XLOPER12
        Dim numFixed As Long = 10
        Dim numArgs As Long
        Dim i As Long
        Dim txt As String
        
        ' First some compatibility checks...
        ' Handle mutually exclusive options
        If Me.MacroEquivalent Then Me.ThreadSafe = False
        ' Make sure that both Me.ProcName and Me.FuncHelp are defined
        If Me.ProcName = vbNullString And Me.FuncHelp <> vbNullString Then
            Me.ProcName = Me.FuncHelp
        ElseIf Me.FuncHelp = vbNullString And Me.ProcName <> vbNullString Then
            Me.FuncHelp = Me.ProcName
        End If
        
        ' Get this XLL's name (DO NOT xlFree this)
        Excel12v xlGetName, xDll(0), 0, dummy

        numArgs = Me.Arguments.Count
        If numArgs < 0 Then numArgs = 0

        ReDim def(0 To numFixed + numArgs) As XLOPER12

        def(0) = xDll(0)
        def(1) = GetXLString12(Me.ProcName)
        
        ' Build the type-text string
        txt = "U" & String(numArgs, "U")
        ' Handle special registration options
        If Me.Volatile Then txt = txt & "!"
        If Me.ThreadSafe Then txt = txt & "$"
        If Me.MacroEquivalent Then txt = txt & "#"
        def(2) = GetXLString12(txt)
        
        def(3) = GetXLString12(Me.FuncText)
        
        ' Build comma-separated argument name list
        txt = ""
        If Me.Arguments.Count > 0 Then
            txt = Me.Arguments.Item(1).Name
            For i = 2 To Me.Arguments.Count
                txt = txt & "," & Me.Arguments.Item(i).Name
            Next i
        End If
        def(4) = GetXLString12(txt) ' Argument text
        
        ' Define function type
        If Me.Visible Then
            def(5) = GetXLString12("1") ' Visible
        Else
            def(5) = GetXLString12("2") ' Hidden
        End If
        
        def(6) = GetXLString12(Me.Category)
        def(7) = GetXLString12(Me.ShortcutText)
        def(8) = GetXLString12(Me.HelpTopic)
        def(9) = GetXLString12(Me.FuncHelp)
        
        ' Handle arument help text entries - one for each argument
        For i = 1 To Me.Arguments.Count
            def(numFixed + i - 1) = GetXLString12(Me.Arguments.Item(i).Help)
        Next i
        
        ' Append dummy arg help string to solve arg help text truncation issue
        def(numFixed + numArgs) = GetXLString12(vbNullString)
        
        Excel12v xlfRegister, ByVal vbNullPtr, UBound(def) + 1, def
        mRegistered = True
    End Sub
    
    [Description("Unregisters the UDF for use as a worksheet function.")]
    Public Sub UnRegister()
        Dim xDll(0) As XLOPER12
        
        If Not mRegistered Then Exit Sub
        ' Get this XLL's name (DO NOT xlFree this)
        Excel12v xlGetName, xDll(0), 0

        ' Unregister visible UDFs
        Dim xOpers(1) As XLOPER12
        Dim xRegId(0) As XLOPER12

        xOpers(0) = xDll(0)
        xOpers(1) = GetXLString12(Me.ProcName)

        Excel12v xlfRegisterId, xRegId(0), 2, xOpers
        Excel12v xlfUnregister, ByVal vbNullPtr, 1, xRegId

        ' xlfRegisterId returns an XLOPER that should be freed
        Excel12v xlFree, ByVal vbNullPtr, 1, xRegId

        ' Workaround Excel bug: re-register as hidden (macro_type = 2)
        ' Visible UDFs must be re-registered as hidden before full unregister
        If Me.Visible Then
            Me.Visible = False
            Me.Register
            Me.UnRegister
            Me.Visible = True
        End If
        mRegistered = False
    End Sub
    
    Private Sub Class_Initialize()
        Set oArguments = New Collection
        mVisible = True
        mThreadSafe = True 'preferred except for Volatile UDFs
    End Sub
    
    ' Helper for freeing def() memory
    Private Sub FreeRegistrationStrings()
        If Not mRegistered Then Exit Sub
        Dim i As Long
        Dim lpStr As LongPtr
        
        ' Start at 1: def(0) is the xlGetName result owned by Excel
        For i = 1 To UBound(def)
            If def(i).xltype = xltypeStr Then
                CopyMemory lpStr, ByVal VarPtr(def(i).val(0)), LenB(Of LongPtr)
                If lpStr <> 0 Then GlobalFree lpStr
                def(i).xltype = xltypeNil
            End If
        Next i
    End Sub
    
    Private Sub Class_Terminate()
        FreeRegistrationStrings
        Set oArguments = Nothing
    End Sub

End Class

Public Class Argument
    Private mName As String
    Private mHelp As String

    [Description("The argument name shown in Excel’s Function Wizard and formula tooltips.")]
    Public Property Let Name(ByVal v As String)
        mName = v
    End Property
    Public Property Get Name() As String
        Name = mName
    End Property

    [Description("The help text shown for this argument in Excel’s Function Wizard.")]
    Public Property Let Help(ByVal v As String)
        mHelp = v
    End Property
    Public Property Get Help() As String
        Help = mHelp
    End Property

End Class
